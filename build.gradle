group 'ink.abb'
version '0.4.1'

buildscript {
    ext {
        kotlin_version = '1.0.3'
        springBootVersion = '1.3.6.RELEASE'
    }

    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'

mainClassName = 'ink.abb.pogo.scraper.MainKt'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {

    compile project(':PokeGOAPI-Java')

    compile 'com.squareup.okhttp3:okhttp:3.4.0-RC1'

    // https://mvnrepository.com/artifact/org.isuper/s2-geometry-library-java
    compile group: 'org.isuper', name: 's2-geometry-library-java', version: '0.0.1'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'org.springframework.boot:spring-boot-starter'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.8.0'
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.8.0'

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile group: 'junit', name: 'junit', version: '4.11'
}
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Pokemon Go Bot',
                'Implementation-Version': version,
                'Main-Class': mainClassName
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
jar {
    baseName = 'PokemonGoBot'
    version = '0.0.1-SNAPSHOT'

    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': mainClassName
        )
    }
}

eclipse {
    classpath {
        containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.8'
    }
}
