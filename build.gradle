group 'ink.abb'
version '1.0.3'

buildscript {
    ext {
        kotlin_version = '1.0.3'
        springBootVersion = '1.3.6.RELEASE'
        jacksonVersion = '2.8.0'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'spring-boot'
apply plugin: 'checkstyle'

mainClassName = 'ink.abb.pogo.scraper.MainKt'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

def sourceResourcesFolder = "$projectDir/src/main/resources"

sourceSets {
    main {
        kotlin {
            srcDir 'src/main/kotlin'
        }
        java {
            srcDir 'lib/com.google.guava'
        }
        resources.srcDirs sourceResourcesFolder
    }
}

checkstyle {
    toolVersion = "7.1"
    checkstyleMain.configFile = new File(rootDir, "checkstyle.xml")
    checkstyleMain.source = "src/main/kotlin"
    checkstyleMain.ignoreFailures = false
    checkstyleMain.showViolations = true
}

configurations {
    compile.exclude module: "spring-boot-starter-tomcat"
}

dependencies {
    //compile files('../PokeGOAPI-Java/library/build/libs/PokeGOAPI-library-all-0.3.0.jar')

    compile files('lib/api-1.1.1.jar')

    // TODO: Wait for jitpack to fix itself...
    //compile 'com.github.Grover-c13:PokeGOAPI-Java:Development-SNAPSHOT'

    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'io.reactivex:rxkotlin:0.60.0'
    compile 'svarzee.gps:gpsoauth:0.3'
    compile 'com.squareup.moshi:moshi:1.2.0'
    compile 'net.jpountz.lz4:lz4:1.3.0'
    compile 'com.google.protobuf:protobuf-java:3.0.0-beta-3'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.corundumstudio.socketio:netty-socketio:1.7.7"

    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-undertow'

    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: jacksonVersion

    compile group: 'com.google.maps', name: 'google-maps-services', version: '0.1.15'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
}

jar {
    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': mainClassName
        )
    }
}

task createProperties(dependsOn: processResources) << {
    String versionDir = "$buildDir/resources/main/ink/abb/pogo/scraper"
    new File(versionDir).mkdirs()
    new File("$versionDir/version.properties").withWriter { w ->
        Properties p = new Properties()
        p['version'] = project.version.toString()
        p.store w, null
    }
}

classes {
    dependsOn createProperties
}
